plugins {
	id 'java'
	id 'org.springframework.boot' version '3.3.3'
	id 'jacoco'
	id 'io.spring.dependency-management' version '1.1.6'
}

group = 'co.edu.gimnasiolorismalaguzzi'
version = '0.0.1-SNAPSHOT'


java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
}

ext {
	set('springCloudVersion', "2023.0.3")
}

dependencyManagement {
	imports {
		mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
	}
}

repositories {
	mavenCentral()
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-web'
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	implementation 'org.springframework.cloud:spring-cloud-starter-netflix-eureka-client'
	implementation 'org.springframework.cloud:spring-cloud-starter-config'

	implementation 'org.projectlombok:lombok'
	implementation 'org.springframework.boot:spring-boot-starter-security'
	testImplementation 'org.springframework.security:spring-security-test'
	annotationProcessor 'org.projectlombok:lombok'
	implementation 'jakarta.validation:jakarta.validation-api:3.0.2'
	implementation 'org.keycloak:keycloak-admin-client:26.0.2'
	implementation 'org.springframework.boot:spring-boot-starter-oauth2-resource-server'
	//implementation 'com.vladmihalcea:hibernate-types-60:2.14.0'
	implementation 'io.hypersistence:hypersistence-utils-hibernate-63:3.9.9'

	//implementation group: 'org.jboss.resteasy', name: 'resteasy-jaxrs', version: '6.2.10.Final'
	// Apache POI - Para trabajar con archivos Excel
	implementation 'org.apache.poi:poi:5.2.3'
	implementation 'org.apache.poi:poi-ooxml:5.2.3'

	// OpenPDF iText open source
	implementation 'com.github.librepdf:openpdf:1.3.30'
	implementation 'org.apache.pdfbox:pdfbox:2.0.27'



	implementation 'org.mapstruct:mapstruct:1.5.2.Final'
	annotationProcessor 'org.mapstruct:mapstruct-processor:1.5.2.Final'


	developmentOnly 'org.springframework.boot:spring-boot-devtools'
	implementation 'org.springframework.boot:spring-boot-starter-data-jpa'
	runtimeOnly 'org.postgresql:postgresql'

	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'
	/*compileOnly 'org.projectlombok:lombok:1.18.28'  // Versión más reciente de Lombok
	annotationProcessor 'org.projectlombok:lombok:1.18.28'*/
}


test {

	testLogging {
		events "passed", "skipped", "failed"
	}

	ignoreFailures = true

    useJUnitPlatform()
    finalizedBy jacocoTestReport // Genera el reporte después de los tests
}

jacoco {
    toolVersion = "0.8.10"
}

jacocoTestReport {
	dependsOn test
	reports {
		xml.required.set(true)  // Para SonarQube
		csv.required.set(false)
		html.outputLocation.set(layout.buildDirectory.dir("jacocoHtml"))
	}

	afterEvaluate {
		classDirectories.setFrom(files(classDirectories.files.collect {
			fileTree(dir: it, includes: [
					// Incluir solo los servicios que te interesan
					'co/edu/gimnasiolorismalaguzzi/academyservice/academic/service/**',
					'co/edu/gimnasiolorismalaguzzi/academyservice/administration/service/**',
					'co/edu/gimnasiolorismalaguzzi/academyservice/evaluation/service/**',
					'co/edu/gimnasiolorismalaguzzi/academyservice/knowledge/service/**',
					'co/edu/gimnasiolorismalaguzzi/academyservice/student/service/**'
			], excludes: [
					// Excluir explícitamente los mappers y otras clases que no quieres incluir
					'**/*Mapper*',
					'**/*Controller*',
					'**/*Entity*',
					'**/*Repository*',
					'**/*Domain*',
					'**/*Exception*',
					'**/*Config*'
			])
		}))
	}
}

jacocoTestCoverageVerification {
	violationRules {
		rule {
			element = 'CLASS'
			includes = [
					'co.edu.gimnasiolorismalaguzzi.academyservice.academic.service.*',
					'co.edu.gimnasiolorismalaguzzi.academyservice.administration.service.*',
					'co.edu.gimnasiolorismalaguzzi.academyservice.evaluation.service.*',
					'co.edu.gimnasiolorismalaguzzi.academyservice.knowledge.service.*',
					'co.edu.gimnasiolorismalaguzzi.academyservice.student.service.*'
			]
			limit {
				counter = 'LINE'
				minimum = 0.80 // Puedes ajustar este valor según tus necesidades
			}
		}
	}
}
