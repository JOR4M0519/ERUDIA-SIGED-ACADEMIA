plugins {
	id 'java'
	id 'org.springframework.boot' version '3.3.3'
	id 'jacoco'
	id 'io.spring.dependency-management' version '1.1.6'
}

group = 'co.edu.gimnasiolorismalaguzzi'
version = '0.0.1-SNAPSHOT'

java {
	toolchain {
		languageVersion = JavaLanguageVersion.of(21)
	}
}

repositories {
	mavenCentral()
}

// Configuración de Spring Cloud
ext {
	set('springCloudVersion', "2023.0.3")
}

dependencyManagement {
	imports {
		mavenBom "org.springframework.cloud:spring-cloud-dependencies:${springCloudVersion}"
	}
}

dependencies {
	implementation 'org.springframework.boot:spring-boot-starter-actuator'
	implementation 'org.springframework.cloud:spring-cloud-starter-config'
	implementation 'org.springframework.cloud:spring-cloud-starter-netflix-eureka-client'
	implementation 'org.springframework.cloud:spring-cloud-starter-gateway'
	implementation 'org.springframework.boot:spring-boot-starter-webflux'
	implementation 'org.springframework.boot:spring-boot-starter-json'

	implementation 'org.projectlombok:lombok'
	implementation 'org.springframework.cloud:spring-cloud-starter-openfeign' // Feign Client
	implementation 'com.fasterxml.jackson.core:jackson-databind' // Jackson para convertir JSON
	implementation 'io.github.openfeign:feign-core' //  Core de Feign
	implementation 'io.github.openfeign:feign-jackson' // Jackson para Feign

	annotationProcessor 'org.projectlombok:lombok'
	implementation 'jakarta.servlet:jakarta.servlet-api:5.0.0'
	implementation 'jakarta.validation:jakarta.validation-api:3.0.2'
	implementation 'org.keycloak:keycloak-admin-client:26.0.2'

	// Añade estas dependencias específicas para JWT y Keycloak
	implementation 'io.jsonwebtoken:jjwt-api:0.11.5'
	implementation 'io.jsonwebtoken:jjwt-impl:0.11.5'
	implementation 'io.jsonwebtoken:jjwt-jackson:0.11.5'
	implementation 'org.keycloak:keycloak-core:26.0.2'
	implementation 'org.keycloak:keycloak-authz-client:26.0.2'
	implementation 'org.jboss.resteasy:resteasy-client:6.2.5.Final'
	implementation 'com.fasterxml.jackson.datatype:jackson-datatype-jsr310'


	implementation 'org.springframework.boot:spring-boot-starter-oauth2-resource-server'
	implementation 'org.springframework.boot:spring-boot-starter-security'

	testImplementation 'org.springframework.boot:spring-boot-starter-test'
	testImplementation 'org.springframework.security:spring-security-test'
	testRuntimeOnly 'org.junit.platform:junit-platform-launcher'


	testImplementation 'org.springframework.boot:spring-boot-starter-test'

	// Dependencias adicionales para pruebas
	testImplementation 'org.junit.jupiter:junit-jupiter-api:5.10.1'
	testImplementation 'org.junit.jupiter:junit-jupiter-engine:5.10.1'
	testImplementation 'org.mockito:mockito-core:5.7.0'
	testImplementation 'org.mockito:mockito-inline:5.2.0'
	testImplementation 'org.mockito:mockito-junit-jupiter:5.7.0'
	testImplementation 'io.projectreactor:reactor-test'


}

// Configuración para ejecutar tests
/*
test {
	useJUnitPlatform()
	testLogging {
		events "passed", "skipped", "failed"
	}
}
*/


test {

	testLogging {
		events "passed", "skipped", "failed"
	}

	ignoreFailures = true

	useJUnitPlatform()
	finalizedBy jacocoTestReport // Genera el reporte después de los tests
}

jacoco {
	toolVersion = "0.8.10"
}

jacocoTestReport {
	dependsOn test
	reports {
		xml.required.set(true)  // Para SonarQube
		csv.required.set(false)
		html.outputLocation.set(layout.buildDirectory.dir("jacocoHtml"))
	}

	afterEvaluate {
		classDirectories.setFrom(files(classDirectories.files.collect {
			fileTree(dir: it, includes: [
					// Incluir solo los servicios que te interesan
					'co/edu/gimnasiolorismalaguzzi/gatewayservice/services/**',
			], excludes: [
					// Excluir explícitamente los mappers y otras clases
					'**/*Mapper*',
					'**/*Controller*',
					'**/*Entity*',
					'**/*Repository*',
					'**/*Domain*',
					'**/*Exception*',
					'**/*Config*'
			])
		}))
	}
}

jacocoTestCoverageVerification {
	violationRules {
		rule {
			element = 'CLASS'
			includes = [
					'co.edu.gimnasiolorismalaguzzi.gatewayservice.services.*',
			]
			limit {
				counter = 'LINE'
				minimum = 0.80 // Puedes ajustar este valor según tus necesidades
			}
		}
	}
}